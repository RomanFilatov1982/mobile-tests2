plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
}

repositories {
    mavenCentral()
}

def aspectjWeaverVersion = "1.9.22",
    selenideVersion = "6.13.0",
    restAssuredVersion = "5.3.0",
    allureVersion = "2.21.0",
    junitVersion = "5.9.3",
    slf4jVersion = "2.0.7"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation(
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.appium:java-client:8.3.0",
            "commons-io:commons-io:2.11.0",
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "org.aeonbits.owner:owner:1.0.12"
    )
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperties(System.getProperties())
    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task android(type: Test) {
    useJUnitPlatform {
        includeTags("android")
    }
    systemProperty "deviceHost", System.getProperty("deviceHost", "remote")
    systemProperty "platform", System.getProperty("platform", "android")
}

task ios(type: Test) {
    useJUnitPlatform {
        includeTags("ios")
    }
    systemProperty "deviceHost", System.getProperty("deviceHost", "remote")
    systemProperty "platform", System.getProperty("platform", "ios")
}

task android_local(type: Test) {
    useJUnitPlatform {
        includeTags("android_local")
    }
    systemProperty "deviceHost", System.getProperty("deviceHost", "local")
}